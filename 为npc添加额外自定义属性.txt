
/** 在这里自行添加/删除自定义属性 */
enum NpcAttributes
{
	AttINT_Start,	// 用于定位int属性的无效值
	AttINT_V1,
	AttINT_V2,
	AttINT_V3,
	AttINT_V4,
	AttINT_End,	// 用于定位int属性的无效值
	AttFL_Start,	// 用于定位float属性的无效值
	AttFL_V1,
	AttFL_V2,
	AttFL_V3,
	AttFL_V4,
	AttFL_End,	// 用于定位float属性的无效值
	AttVEC_Start,	// 用于定位vector属性的无效值
	AttVEC_V1,
	AttVEC_V2,
	AttVEC_V3,
	AttVEC_V4,
	AttVEC_End	// 用于定位vector属性的无效值
}

/** npc自定义属性初始化 */
public npc_Attributes_Init(ent)
{
	new Array:arrayAttributes = ArrayCreate(1, 3)									// 创建用于存放自定义属性的动态数组
	set_pev(ent, pev_iuser1, arrayAttributes)									// 将arrayAttributes存入pev_iuser1
	
	new Array:arrayInt = ArrayCreate(1,  any:(AttINT_End - AttINT_Start) - 1)					// 创建用于存放int属性的动态数组
	ArrayPushCell(arrayAttributes, arrayInt)									// 将int动态数组存入arrayAttributes
	for (new i = any:AttINT_Start + 1; i < any:AttINT_End; i++) ArrayPushCell(arrayInt, 0)				// 生成int属性的默认值

	new Array:arrayFloat = ArrayCreate(1,  any:(AttFL_End - AttFL_Start) - 1)					// 创建用于存放float属性的动态数组
	ArrayPushCell(arrayAttributes, arrayFloat)									// 将float动态数组存入arrayAttributes
	for (new i = any:AttFL_Start + 1; i < any:AttFL_End; i++) ArrayPushCell(arrayFloat, 0)				// 生成float属性的默认值

	new Array:arrayVector = ArrayCreate(3, any:(AttVEC_End - AttVEC_Start) - 1)					// 创建用于存放vector属性的动态数组
	ArrayPushCell(arrayAttributes, arrayVector)									// 将vector动态数组存入arrayAttributes
	for (new i = any:AttVEC_Start + 1; i < any:AttVEC_End; i++) ArrayPushArray(arrayVector, Float:{0.0, 0.0, 0.0})	// 生成vector属性的默认值
}

/** 获取npc自定义属性
 *  例子1:	new intValue = npc_GetAttribute(ent, AttINT_V1);
 *  例子2:	new Float:floatValue = npc_GetAttribute(ent, AttFL_V1);
 *  例子3:	new Float:vector[3]; npc_GetAttribute(ent, AttVEC_V1, vector);
 */
public any:npc_GetAttribute(ent, NpcAttributes:att, any:...)
{
	if (AttINT_Start < att < AttINT_End) return ArrayGetCell(ArrayGetCell(any:pev(ent, pev_iuser1), 0), any:(att - AttINT_Start) - 1)
	if (AttFL_Start < att < AttFL_End) return ArrayGetCell(ArrayGetCell(any:pev(ent, pev_iuser1), 1), any:(att - AttFL_Start) - 1)
	if (AttVEC_Start < att < AttVEC_End)
	{
		new Float:vector[3]
		ArrayGetArray(ArrayGetCell(any:pev(ent, pev_iuser1), 2), any:(att - AttVEC_Start) - 1, vector)
		setarg(2, 0, any:vector[0])
		setarg(2, 1, any:vector[1])
		setarg(2, 2, any:vector[2])
	}
	return 0
}

/** 修改npc自定义属性
 *  例子1:	npc_SetAttribute(ent, AttINT_V1, 100);
 *  例子2:	npc_SetAttribute(ent, AttFL_V1, 250.0);
 *  例子3:	npc_SetAttribute(ent, AttVEC_V1, Float:{0.0, 100.0, 0.0});
 */
public npc_SetAttribute(ent, NpcAttributes:att, any:...)
{
	if (AttINT_Start < att < AttINT_End) ArraySetCell(ArrayGetCell(any:pev(ent, pev_iuser1), 0), any:(att - AttINT_Start) - 1, getarg(2))
	if (AttFL_Start < att < AttFL_End) ArraySetCell(ArrayGetCell(any:pev(ent, pev_iuser1), 1), any:(att - AttFL_Start) - 1, getarg(2))
	if (AttVEC_Start < att < AttVEC_End)
	{
		new Float:vector[3]
		vector[0] = any:getarg(2, 0)
		vector[1] = any:getarg(2, 1)
		vector[2] = any:getarg(2, 2)
		ArraySetArray(ArrayGetCell(any:pev(ent, pev_iuser1), 2), any:(att - AttVEC_Start) - 1, vector)
	}
}

/** 销毁npc自定义属性 */
public npc_Attributes_Destroy(ent)
{
	new Array:arrayAttributes = any:pev(ent, pev_iuser1)
	new Array:arrayInt = ArrayGetCell(arrayAttributes, 0)
	new Array:arrayFloat = ArrayGetCell(arrayAttributes, 1)
	new Array:arrayVector = ArrayGetCell(arrayAttributes, 2)
	ArrayDestroy(arrayInt)
	ArrayDestroy(arrayFloat)
	ArrayDestroy(arrayVector)
	ArrayDestroy(arrayAttributes)
}